group = 'com.bluecatcode.common'
version = '17.0.0-SNAPSHOT'

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
}

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
    }
}

subprojects { project ->

    apply plugin: 'java'

    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }

    apply plugin: "groovy"

    dependencies {
        // mandatory dependencies for using Spock
        testCompile "org.codehaus.groovy:groovy-all:2.3.3"
        testCompile "org.spockframework:spock-core:0.7-groovy-2.0"

        // optional dependencies for using Spock
        testCompile group: 'org.hamcrest',         name: 'hamcrest-core',    version: '1.3'
        testCompile group: 'org.hamcrest',         name: 'hamcrest-library', version: '1.3'

        // allows mocking of classes (in addition to interfaces)
        testRuntime group: 'cglib',                name: 'cglib-nodep',      version: '2.2.2'
        // allows mocking of classes without default constructor (together with CGLIB)
        testRuntime group: 'org.objenesis',        name: 'objenesis',        version: '1.4'
    }

    jar {
        manifest.attributes provider: 'gradle'
    }
}

project(':core') {

    dependencies {
        compile group: 'com.google.guava',         name: 'guava',            version: '17.0'
        compile group: 'com.google.code.findbugs', name: 'jsr305',           version: '2.0.3'
        compile group: 'javax.mail',               name: 'mail',             version: '1.4.7'
    }
}
